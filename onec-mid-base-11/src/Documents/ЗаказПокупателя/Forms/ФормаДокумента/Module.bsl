
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	//>>Netology: Добавление поля "Контактное лицо", "Скидка" и команды пересчета суммы при применении скидки
	ПолеКонтактноеЛицо = Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), Элементы.ГруппаШапкаПраво);
	ПолеКонтактноеЛицо.Вид = ВидПоляФормы.ПолеВвода;
	ПолеКонтактноеЛицо.ПутьКДанным = "Объект.Д_КонтактноеЛицо";
	
	ГруппаСкидки = Элементы.Добавить("ГруппаСкидки", Тип("ГруппаФормы"), Элементы.ГруппаШапкаПраво);
	ГруппаСкидки.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаСкидки.Заголовок = "Расчет скидки";
	
	ПолеСкидки = Элементы.Добавить("СогласованнаяСкидка", Тип("ПолеФормы"), ГруппаСкидки);
	ПолеСкидки.Вид = ВидПоляФормы.ПолеВвода;
	ПолеСкидки.ПутьКДанным = "Объект.Д_СогласованнаяСкидка";
	ПолеСкидки.УстановитьДействие("ПриИзменении","Д_СогласованнаяСкидкаПриИзменении");
	
	Команда = Команды.Добавить("ПересчитатьСумму");
	Команда.Заголовок = "Пересчитать сумму";
	Команда.Действие = "КомандаПересчитатьСумму";
	
	КнопкаФормы = Элементы.Добавить("КнопкаПересчитатьСумму", Тип("КнопкаФормы"), ГруппаСкидки);
	КнопкаФормы.ИмяКоманды = "ПересчитатьСумму";
	КнопкаФормы.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	//<<
	
КонецПроцедуры

&НаКлиенте
Процедура Д_СогласованнаяСкидкаПриИзменении(Элемент)

	Если Объект.Товары.Количество() <> 0 или Объект.Услуги.Количество() <> 0 Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("РасчетОтОтветаПользователя", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, "Изменен процент скидки. Пересчитать таблицы товаров и услуг?", РежимДиалогаВопрос.ДаНет);
	КонецЕсли;

КонецПроцедуры


&НаКлиенте
Процедура КомандаПересчитатьСумму(Команда);
	
	//>>Netology: Действие команды пересчета суммы с применением скидки
		РезультатВопроса = КодВозвратаДиалога.Да;
		ДопПараметр = Неопределено;
		РасчетОтОтветаПользователя(РезультатВопроса, ДопПараметр);
	//<<
	
КонецПроцедуры

&НаКлиенте
Процедура РасчетОтОтветаПользователя(РезультатВопроса, ДопПараметр) Экспорт
	
	//>>Netology: Действие команды пересчета суммы с применением скидки
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Для Каждого Строка Из Объект.Товары Цикл
			Строка.Сумма = Строка.Цена * Строка.Количество * (100-Объект.Д_СогласованнаяСкидка)/100;
		КонецЦикла;
		Для Каждого Строка Из Объект.Услуги Цикл
			Строка.Сумма = Строка.Цена * Строка.Количество * (100-Объект.Д_СогласованнаяСкидка)/100;
		КонецЦикла;
		РассчитатьСуммуДокумента();
	Иначе
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("Сумма по документу не изменилась");
		Сообщение.Сообщить();
	КонецЕсли;
	//<<

КонецПроцедуры


&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
    ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РассчитатьСуммуСтроки(ТекущиеДанные)
	
	//>>YuL Меняем расчет суммы со скидкой
	КоэффициентСкидки = 1 - Объект.Д_СогласованнаяСкидка / 100;
	ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество * КоэффициентСкидки;
	//<<
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуДокумента()
	
	Объект.СуммаДокумента = Объект.Товары.Итог("Сумма") + Объект.Услуги.Итог("Сумма");
	
КонецПроцедуры

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#КонецОбласти
